#!/bin/zsh

# CONFIGURATION #############################################################
local large_size=18
local small_size=7
#############################################################################

autoload +X parse_cmdline help err

local USAGE="%Busage:%b $0 [size]"
local DESC="Set konsole font size."
local LONGHELP
LONGHELP=$(<<END
If called without argument defaults to normal font size defined in
Default.profile. Arguments may be one of %Unormal%u, %Ularge%u or %Usmall%u,
and is case insensitive.
END
)

local konsole_profile=$HOME/.local/share/konsole/Default.profile
local font_setting=$(grep '^Font=' $konsole_profile)
local size=''
local HELP=false
local SUMMARY=false
if ! parse_cmdline '=size h|help|long-help>HELP H|short-help|summary>SUMMARY' $@; then
  print -P "$USAGE"
  return 1
fi
help $HELP $SUMMARY $0 $USAGE $DESC $LONGHELP && return

case $size in
  large|LARGE)
    echo -en "\e]50;${(S)font_setting/,*,/,${large_size},}\007" && sleep 0.1 || :
    ;;
  small|SMALL)
    echo -en "\e]50;${(S)font_setting/,*,/,${small_size},}\007" && sleep 0.1 || :
    ;;
  normal|NORMAL|'')
    echo -en "\e]50;${font_setting}\007" && sleep 0.1 || :
    ;;
  *)
    return 1
    ;;
esac
