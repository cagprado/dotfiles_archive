#!/bin/zsh

autoload +X parse_cmdline help war err

local USAGE="%Busage:%b $0 colorscheme"
local DESC="Set terminal color scheme."

local HELP=false
local SUMMARY=false
local SCHEME='tomorrow'
if ! parse_cmdline '.SCHEME h|help|long-help>HELP H|short-help|summary>SUMMARY' $@; then
	print -P "$USAGE"
	return 1
fi
help $HELP $SUMMARY $0 $USAGE $DESC $LONGHELP && return

local -A KONSOLE_SCHEMES
local -A CONSOLE_SCHEMES
local -A TERMITE_SCHEMES
KONSOLE_SCHEMES+=(tomorrow 'echo -ne "\e]50;colors=Tomorrow\007" && sleep 0.1')
CONSOLE_SCHEMES+=(tomorrow "$(<<'END'
  tput setab 7
  tput setaf 0
  echo -ne "\e]P04d4d4c\e]P8000000"
  echo -ne "\e]P1c82829\e]P9961e20"
  echo -ne "\e]P2718c00\e]PA495a00"
  echo -ne "\e]P3f5871f\e]PBc36b18"
  echo -ne "\e]P44271ae\e]PC2f527c"
  echo -ne "\e]P58959a8\e]PD603e76"
  echo -ne "\e]P63e999f\e]PE2a6a6d"
  echo -ne "\e]P7fafafa\e]PF8e908c"
  echo -ne "\e[8]\e[1;15]"
  clear
END
)")
TERMITE_SCHEMES+=(tomorrow "$(<<'END'
  echo -ne "\e]11;rgb:fa/fa/fa\e\\"   # background
  echo -ne "\e]10;rgb:4d/4d/4c\e\\"   # foreground
  #echo -ne "\e]5;0;rgb:4d/4d/4c\e\\"  # bold
  echo -ne "\e]4;0;rgb:4d4d4c\e\\"; echo -ne "\e]4;8;rgb:4d4d4c\e\\"
  echo -ne "\e]4;1;rgb:c82829\e\\"; echo -ne "\e]4;9;rgb:c82829\e\\"
  echo -ne "\e]4;2;rgb:718c00\e\\"; echo -ne "\e]4;10;rgb:718c00\e\\"
  echo -ne "\e]4;3;rgb:f5871f\e\\"; echo -ne "\e]4;11;rgb:f5871f\e\\"
  echo -ne "\e]4;4;rgb:4271ae\e\\"; echo -ne "\e]4;12;rgb:4271ae\e\\"
  echo -ne "\e]4;5;rgb:8959a8\e\\"; echo -ne "\e]4;13;rgb:8959a8\e\\"
  echo -ne "\e]4;6;rgb:3e999f\e\\"; echo -ne "\e]4;14;rgb:3e999f\e\\"
  echo -ne "\e]4;7;rgb:fafafa\e\\"; echo -ne "\e]4;15;rgb:8e908c\e\\"
  echo -ne "\e]12;7\e\\"
END
)")

if [[ $TERM =~ "konsole" ]]; then
  (( $+KONSOLE_SCHEMES[$SCHEME] )) && eval "$KONSOLE_SCHEMES[$SCHEME]" && return
elif [[ $TERM =~ "termite" ]]; then
  (( $+TERMITE_SCHEMES[$SCHEME] )) && eval "$TERMITE_SCHEMES[$SCHEME]" && return
elif [[ $TERM =~ "linux" ]]; then
  (( $+CONSOLE_SCHEMES[$SCHEME] )) && eval "$CONSOLE_SCHEMES[$SCHEME]" && return
else
  err "I don't know how to set colors for $TERM..."
  return 1
fi

err "$SCHEME is not defined..."
return 1
