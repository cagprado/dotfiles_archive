#!/bin/zsh

autoload +X parse_cmdline help

# PARSE COMMAND LINE ########################################################
local NAME="$0"
local DESC="Shortcut for sshfs. Mount/Umount and login via ssh."
local USAGE="%Busage:%b $NAME [options]"
local LONGHELP=$(<<END
Default behavior is to start a ssh session using host given by %B-s%b option (see
below).

Available options are:
  %B-m|-u%b
      Do not start session, instead, mount (%B-m%b) or umount (%B-u%b) sshfs. Requires
      %B-p%b option to be provided.
  %B-p MOUNTPATH%b
      Mount point for sshfs, needed if either of %B-m%b or %B-u%b is used.
  %B-s USER@HOST%b
      Address of ssh host with username. Needed if %B-m%b is used.
END
)
local MOUNTPATH=""
local HOST=""
local MOUNT=false
local UMOUNT=false
local HELP=false
local SUMMARY=false
local ERROR=false

if !  parse_cmdline 'h|help>HELP H|summary>SUMMARY m>MOUNT u>UMOUNT :p>MOUNTPATH :s>HOST' $@; then
  print -P "Error parsing command line...\n$USAGE"
  return 1
fi
help $HELP $SUMMARY $NAME $USAGE $DESC $LONGHELP && return

if $MOUNT; then
  if $UMOUNT || [[ -z $MOUNTPATH || -z $HOST ]]; then
    ERROR=true
  else
    mkdir -p "$MOUNTPATH"
    sshfs -o idmap=user -o follow_symlinks -o transform_symlinks "$HOST:" "$MOUNTPATH" -C || return $?
  fi

elif $UMOUNT; then
  if [[ -z $MOUNTPATH ]]; then
    ERROR=true
  else
    fusermount -u "$MOUNTPATH" && rmdir "$MOUNTPATH" >/dev/null 2>&1 || return $?
  fi

else
  if [[ -z $HOST ]]; then
    ERROR=true
  else
    ssh $HOST
  fi
fi

if $ERROR; then
  print -P "Error parsing command line...\n$USAGE"
  return 1
fi
