#!/bin/zsh

local MAINMONITOR="LVDS1"

autoload +X parse_cmdline help err war

local USAGE="%Busage:%b $0 [option]"
local DESC="Set resolution and positon of multiple monitors in X."
local LONGHELP=$(<<END
Default behaviour is to check all connected monitors and set them to their
preferred behaviour. This can be changed with the available options:
  %B-s%b
      Single mode: This is default when a single monitor is connected. If
      multiple options are available, assume the first one that is not the
      laptop screen, useful for a TV setup.
  %B-x%b
      Clone mode: All screens will have the same output. Resolution is scaled
      for different resolution monitors.

%BKNOWN ISSUES%b
  This script has only been tested with a maximum of two available monitors.
  It may result in unexpected behaviour when using more monitors.
END
)

local HELP=false
local SUMMARY=false
local SINGLE=false
local CLONE=false
local -a MONITORS PRIORITY
if ! parse_cmdline 'h|help|long-help>HELP H|short-help|summary>SUMMARY s>SINGLE x>CLONE' $@; then
  print -P "$USAGE"
  return 1
fi
help $HELP $SUMMARY $0 $USAGE $DESC $LONGHELP && return

if [[ "$CLONE" == "false" ]]; then
  local PRIORITY=("HDMI1" "DP1" "VGA1")

  if [[ "$SINGLE" == "false" ]]; then
    # Auto (multiple monitors, or no external monitor), primary is laptop
    PRIORITY=($MAINMONITOR $PRIORITY)
  else
    # Single (forced), laptop is fallback
    PRIORITY+=$MAINMONITOR
  fi

  # Parse list of available monitors
  MONITORS=$(xrandr -q | sed -n -e 's/^\([^ ]*\) \(disconnected\).*/--output \1 --off/p' -e 's/^\([^ ]*\) \(connected\).*/--output \1 --auto/p')
  MONITORS=(${(f)MONITORS})  # (f) split at \n

  # Look for the primary monitor
  local ITEM
  for ITEM in $PRIORITY; do
    local IDX=${MONITORS[(i)*${ITEM} --auto]}
    if [[ $IDX -le $#MONITORS ]]; then
      MONITORS[$IDX]+=' --primary'
      break
    fi
  done

  # If single mode is used turn off all but priority
  [[ $SINGLE = "true" ]] && MONITORS=(${MONITORS/%--auto/--off})

  xrandr $=MONITORS
else
  [[ "$SINGLE" == "true" ]] && war "Clone mode used, ignoring -s flag..."
  echo "Not implemented clone mode..."
# xrandr --output $PRIMARY --auto --output $SECONDARY --auto --same-as $PRIMARY --scale (x1/x2)x(y1/y2)
fi
