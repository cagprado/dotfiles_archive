pkgname=ttf-iosevka-custom
pkgver=20210616
pkgrel=1
pkgdesc='My own customization of the Iosevka Font'
arch=('any')
url='https://be5invis.github.io/Iosevka/'
license=('custom:OFL')
makedepends=('git' 'nodejs>=12.16.0' 'npm' 'ttfautohint')
source=(
  'https://github.com/be5invis/Iosevka/archive/master.zip'
  'private-build-plans.toml'
)
sha256sums=(
  'SKIP'
  '4abffc190d9ea749918fa5ece08e5c4b20cfe0c452ff1d8fb6d4b5af74cead5c'
)

pkgver() {
  date +%Y%m%d
}

prepare() {
  cp private-build-plans.toml Iosevka-master
}

build() {
  cd Iosevka-master
  npm install
  npm update
  npm run build -- --jCmd 6 ttf::${pkgname#*-}
}

package() {
  fontdir="${pkgdir}/usr/share/fonts/TTF"
  install -d "${fontdir}"
  install -m644 Iosevka-master/dist/*/ttf/*.ttf "${fontdir}"

  licensedir="${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${licensedir}"
  install -m644 Iosevka-master/LICENSE.md "${licensedir}"
}
