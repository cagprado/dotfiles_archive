# vim: tw=0
# Maintainer: William Di Luigi <williamdiluigi@gmail.com>

pkgname=latexrun
pkgver=r134.38ff6ec
pkgrel=1
pkgdesc="A 21st century LaTeX wrapper"
arch=('any')
url="https://github.com/aclements/latexrun"
license=('MIT')
depends=('python')
makedepends=('git')

source=('git://github.com/aclements/latexrun.git' 'latexrun.fix-braces-parsing.diff')
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd $srcdir/latexrun
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  local file
  cd $srcdir/latexrun
  for file in "${source[@]}"; do
    [[ "$file" == *.diff ]] && git apply "$srcdir/$file"
  done
}

package() {
  cd $srcdir/latexrun

  # Copy the main script
  install -D -m755 latexrun $pkgdir/usr/bin/latexrun
}
